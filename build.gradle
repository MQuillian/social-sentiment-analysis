plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'base'
    id 'com.palantir.docker' version '0.27.0'
    id 'com.palantir.docker-run' version '0.27.0'
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
}

repositories {
    mavenCentral()
}

group = 'com.sentiment'
version = scmVersion.version
sourceCompatibility = '11'

ext {
    set('springCloudVersion', "2020.0.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework:spring-core:5.3.9'
    implementation 'org.springframework.data:spring-data-elasticsearch:4.2.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'edu.stanford.nlp:stanford-corenlp:4.2.2'
    implementation group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '4.2.2', classifier: 'models'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    enabled = true;
}

String imageName = "mquillian/sentiment:$version"

docker {
    name imageName
    files 'build/libs'
}

dockerRun {
    name project.name
    image imageName
    ports '8081:8080'
    clean true
    daemonize false
}